//
// @description MIDI note horn-type modulation
// @version 1.0.0
// @author Shawn Bulen
// @about  Will add mod wheel events over time, increasing vibrato on long-held notes.  Not all voices do that, so adds a little character to dull tones...  This particular script works on all selected takes/tracks.  Not required to be working within the MIDI Editor, but your MIDI notes need to be selected.
//

// Some vars & user prompts...
minccval = 10;
maxccval = 127;
minnoteticks = 960;
startpct = 0.2;
endpct = 0.6;
shape_id = 2;
beztension = 0.2;
controller = 1;
#retvals = "";

sprintf(#retvals, "%d,%d,%d,%f,%f,%d,%f,%d", minccval, maxccval, minnoteticks, startpct, endpct, shape_id, beztension, controller);
GetUserInputs("Insert Modulations", 7, "Start CC value, End CC value, Min duration in ticks, Ramp start, Ramp end, Shape, Bez Tension, Controller", #retvals);

match("%d,%d,%d,%f,%f,%d,%f,%d", #retvals, minccval, maxccval, minnoteticks, startpct, endpct, shape_id, beztension, controller);

Undo_BeginBlock();

// Get the list of selected MIDI notes
// Loop thru MediaItems, then Takes, then Notes
numItems = CountMediaItems(0);
itemidx = 0;
loop (numItems,
  item = GetMediaItem(0, itemidx);
  IsMediaItemSelected(item) ? (
    lastpos = 0;
    take = GetActiveTake(item);
    MIDI_DisableSort(take);

    noteidx = MIDI_EnumSelNotes(take, -1);
    while (noteidx != -1) (
      MIDI_GetNote(take, noteidx, selected, muted, startppqpos, endppqpos, chan, pitch, vel);

      // Try & handle overlapping notes somewhat gracefully...
      ((selected == 1) && (startppqpos >= lastpos)) ? (
        // Always bring start down to min, even for short notes, when they don't overlap
        MIDI_InsertCC(take, selected, muted, startppqpos, 0xB0, chan, controller, minccval);

        // If note is long enough, do the ramp...
        ((endppqpos - startppqpos) >= minnoteticks) ? (
          startramp = startppqpos + floor((endppqpos - startppqpos) * startpct);
          endramp =  startppqpos + floor((endppqpos - startppqpos) * endpct);
          lastpos = endppqpos;

          MIDI_InsertCC(take, selected, muted, startramp, 0xB0, chan, controller, minccval);

          // Set shape on the ramp CC event; need to find last event inserted...
          MIDI_CountEvts(take, notecnt, ccevtcnt, textsyxevtcnt);
          MIDI_SetCCShape(take, ccevtcnt - 1, shape_id, beztension, true);

          MIDI_InsertCC(take, selected, muted, endramp, 0xB0, chan, controller, maxccval);
          MIDI_InsertCC(take, selected, muted, endppqpos, 0xB0, chan, controller, maxccval);
        );  // doing ramp
      );  // no overlap

      noteidx = MIDI_EnumSelNotes(take, noteidx);
    );  // per selected note

    MIDI_SORT(take);
  );  // is item selected
  itemidx += 1;
);  // per item

Undo_EndBlock("Horn Modulation", 0);

